################################################################################
#
# RevBayes Example: Bayesian inference of phylogeny using a Covarion model
# 
# This file: Runs the full MCMC on a single gene under the Covarion 
#			 
#            subsitution model using GTR and an unconstrained (unrooted) tree model.
#
# authors: Lyndon M. Coghill and Sebastian Hoehna
#
################################################################################

#######################
# Reading in the Data #
#######################

###### This just defines a single model for all sites #######

### Read in sequence data for both genes

data <- readDiscreteCharacterData("data/primates_cytb.nex")

# Get some useful variables from the data. We need these later on.
n_species <- data.ntaxa()
n_sites <- data.nchar()
names <- data.names()
n_branches <- 2 * n_species - 3

# set my move index
mi = 0



######################
# Substitution Model #
######################

#### specify the GTR substitution model applied uniformly to all sites ###
er_prior <- v(1,1,1,1,1,1)

# MrBayes uses the Dirichlet as a prior now
er ~ dnDirichlet(er_prior)
moves[++mi] = mvSimplexElementScale(er,weight=3)


pi_prior <- v(1,1,1,1) 
# see above
pi ~ dnDirichlet(pi_prior)
moves[++mi] = mvSimplexElementScale(pi,weight=2)

# Define a GTR Rate Matrix
Q := fnGTR(er,pi)

# Set our RateScalar. The entire RateMatrix will be multiplied by these values for each SwitchRate Category. 
# To set it to On/Off we set one RateScalar to 0 and one to 1. 
rs = [0.0, 1.0]
## needed to switch from a Probablity type to a RealPos because.... RevBayes.
rs = rs + 0.0

## Setting our SwitchRate values. This should be a Uniform distribution between 0, 100 as it is in MrBayes.
sr1 ~ dnUniform(1, 100)
sr2 ~ dnUniform(1, 100)
sr := [ rep(sr1,4), rep(sr2,4)]

# Actually building the Q Matrix
QC := fnCovarion([Q,Q], rs, sr, false)

##############
# Tree model #
##############

#### Specify a uniform prior on the tree topology #### 
topology ~ dnUniformTopology(names)

# moves on the tree
moves[++mi] = mvNNI(topology)
moves[++mi] = mvSPR(topology)


#### Specify a prior and moves on the branch lengths #### 
# create a random variable for each branch length using a for loop
for (i in 1:n_branches) {
  # We use here the exponential distribution with rate 1.0 as the branch length prior
  br_lens[i] ~ dnExponential(10.0)
  
  # Add a move for the branch length. We just take a simple scaling move since the value is a positive real number.
  moves[++mi] = mvScale(br_lens[i]) 
}

TL := sum(br_lens)

# Build the tree by combining the topology with the branch length.
phylogeny := treeAssembly(topology, br_lens)


###################
# PhyloCTMC Model #
###################


# the sequence evolution model
seq ~ dnPhyloCTMC(tree=phylogeny, Q=QC, "NaturalNumbers")

# expand and attach the data
data_exp <- data.expandCharacters( 2 )
seq.clamp(data_exp)


#############
# THE Model #
#############

# We define our model.
# We can use any node of our model as a handle, here we chose to use the rate matrix.
mymodel = model(QC)


monitors[1] = mnModel(filename="output/primates_cytb_Covarion_posterior.log",printgen=10, separator = TAB)
monitors[2] = mnFile(filename="output/primates_cytb_Covarion_posterior.trees",printgen=10, separator = TAB, phylogeny)
monitors[3] = mnScreen(printgen=1000, TL)

mymcmc = mcmc(mymodel, monitors, moves)

mymcmc.burnin(generations=10000,tuningInterval=1000)
mymcmc.run(generations=30000)


# Now, we will analyze the tree output.
# Let us start by reading in the tree trace
treetrace = readTreeTrace("output/primates_cytb_Covarion_posterior.trees", treetype="non-clock")
# and get the summary of the tree trace
treetrace.summarize()

mapTree(treetrace,"output/primates_cytb_Covarion.tree")

# you may want to quit RevBayes now
q()